module namespace

imports
  assignment1/MiniJava
  trans/analysis/desugar
  
namespaces 
	
  MainClass
  Class
  Method
  Var
  Field
  Program
  THIS
  
  binding rules
  
  Program(_, _): scopes Class
 
  MainClass(v,_,_) : defines MainClass v of type MainClassType() 
  
  Class(c, _, _, _): defines Class c of type ClassType(c)
  					 implicitly defines THIS This()	of type ClassType(c)
  					 scopes Method,Field,THIS
  
  This(): refers to THIS This()
  					 
  Parent(c) : refers to Class c otherwise refers to MainClass c
  NewObject(c) : refers to Class c otherwise refers to MainClass c
  
  ClassType(c): refers to Class c otherwise refers to MainClass c
  
  Method(t,m,ps,_,_,_): defines Method m of type (ts,t)			 
  					   where ps has type ts 
  					   scopes Var	
  					 
  Field(t,v) : defines Field v of type t
  
  Var(t,v) : defines Var v of type t
  Param(t,v) : defines Var v of type t
  
  Call(c,m,_): refers to Method m in Class cn
  				where c has type ClassType(cn)
    			 
  VarRef(v) : refers to Var v otherwise refers to Field v
  
 